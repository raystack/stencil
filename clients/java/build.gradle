plugins {
    id 'idea'
    id 'java-library'
    id 'maven'
    id 'signing'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.8.15'
    id 'io.freefair.lombok' version '5.3.3.3'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group 'io.odpf'
version '0.3.2'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    api group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    api group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
    implementation group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
    api group: 'com.datadoghq', name: 'java-dogstatsd-client', version: '2.13.0'
    implementation group: 'org.json', name: 'json', version:'20211205'

    testImplementation group: 'junit', name: 'junit' , version: '4.12'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.18.0'
    testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '12.0-rc2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.1.0'
    testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/test/generated"
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1"
    }
    generateProtoTasks {
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.includeSourceInfo = false
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.path = "$projectDir/src/test/resources/__files/descriptors.bin"
        }
    }
}

idea {
    module {
        testSourceDirs += file("$projectDir/src/generated/test/java")
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            pom {
                name = 'Stencil'
                description = 'Library to pull latest proto descriptors from a remote server'
                url = 'https://github.com/odpf/stencil'

                scm {
                    url = 'https://github.com/odpf/stencil.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'harikrishnakanchi'
                        name = 'Hari Krishna'
                        email = 'harikrishnakanchi@gmail.com'
                    }
                    developer {
                        id = 'ravisuhag'
                        name = 'Ravi Suhag'
                        email = 'suhag.ravi@gmail.com'
                    }
                }
                from components.java
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("MAVEN_USERNAME")
            password = System.getenv("MAVEN_PASSWORD")
        }
    }
}
